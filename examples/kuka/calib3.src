&ACCESS RVP
&REL 2
DEF calib3()
;------ Declaration section ------
EXT  BAS (BAS_COMMAND  :IN,REAL  :IN )
INT I,BCO_VELOCITY, Index
REAL SETUP_VELOCITY,TRACK_VELOCITY
POS SAFE, HOME
;---------- Initialization ---------
;...........SETUP VELOCITIES........
BCO_VELOCITY = 5 ; PERCENT
SETUP_VELOCITY = .5 ; M/S
TRACK_VELOCITY = .05 ; M/S
;...................................

BAS (#INITMOV,0 ) ;Initialization of velocities,
;accelerations, $BASE, $TOOL, etc.

$ADVANCE = 1 ;How far ahead on the motions the controller can look to do blends
$APO.CDIS = 75 ;DISTANCE FROM POINT TO BEGIN BLEND IN MM

FOR I=1 TO  6
$VEL_AXIS[I] = BCO_VELOCITY ; max axis velocity %
$ACC_AXIS[I] = 10 ; max axis accel %
ENDFOR

$VEL.CP = SETUP_VELOCITY ; linear velocity m/s
$ACC.CP = .1 ; linear accel m/s^2

$BASE = {FRAME: X -0.16,Y 2123.410,Z 620.810,A -182.360,B 0.0,C 87.630}
$TOOL = {FRAME: X 0.0,Y 0.0,Z 0.0,A 90.0,B 0.0,C 0.0}

SAFE = {X 0,Y 700,Z 0,A 0,B 0,C 0,S 'B110',T 'B011011'}
HOME = {X 0,Y 0,Z 0,A 0,B 0,C 0} ; right in front of the table
;----------- Main section ----------
PTP $POS_ACT
LIN  SAFE C_DIS ; BCO run

FOR Index = 1 TO 3
    PTP CalibrationPoints[Index] C_PTP
    WAIT SEC 5
ENDFOR

LIN SAFE C_DIS
END